import org.jetbrains.kotlin.gradle.tasks.KotlinCompile

plugins {
	id 'org.springframework.boot' version '3.2.5'
	id 'io.spring.dependency-management' version '1.1.4'
	id 'org.jetbrains.kotlin.jvm' version '1.9.23'
	id 'org.jetbrains.kotlin.plugin.spring' version '1.9.23'
	id 'org.jetbrains.kotlin.plugin.jpa' version '1.9.23'
	id 'jacoco'
	id "de.undercouch.download" version "5.3.0"
}


java {
	sourceCompatibility = '21'
}

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
	ktlint
}

repositories {
	mavenCentral()
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'com.fasterxml.jackson.module:jackson-module-kotlin'
	implementation 'org.jetbrains.kotlin:kotlin-reflect'
	implementation 'org.springframework.boot:spring-boot-starter-oauth2-resource-server'
	implementation 'org.springframework.boot:spring-boot-starter-security'
	compileOnly 'org.projectlombok:lombok'
	annotationProcessor 'org.projectlombok:lombok'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	ktlint("com.pinterest.ktlint:ktlint-cli:1.2.1") {
		attributes {
			attribute(Bundling.BUNDLING_ATTRIBUTE, getObjects().named(Bundling, Bundling.EXTERNAL))
		}
	}
	implementation 'org.postgresql:postgresql:42.7.3'
	implementation 'org.springframework.boot:spring-boot-starter-validation:3.2.5'
	implementation("jakarta.validation:jakarta.validation-api:3.0.2")

	implementation "org.springdoc:springdoc-openapi-starter-webmvc-api:2.5.0"
	implementation "org.springdoc:springdoc-openapi-starter-webmvc-ui:2.5.0"

	implementation("com.newrelic.agent.java:newrelic-agent:7.2.0")

	implementation 'io.projectreactor:reactor-core:3.4.10'
	implementation 'org.springframework.boot:spring-boot-starter-webflux'
	implementation 'org.springframework.boot:spring-boot-starter-logging'

	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testImplementation 'io.projectreactor:reactor-test'
	testImplementation 'com.h2database:h2:2.2.224'
	testImplementation 'org.springframework.security:spring-security-test'
}

tasks.withType(KotlinCompile) {
	kotlinOptions {
		freeCompilerArgs += '-Xjsr305=strict'
		jvmTarget = '21'
	}
}

tasks.named('test') {
	useJUnitPlatform()
}

tasks.register("checkLint", JavaExec) {
	group = "verification"
	description = "Check Kotlin code style."
	classpath = configurations.ktlint
	mainClass = "com.pinterest.ktlint.Main"
	args "src/**/*.kt", "**.kts", "!**/build/**"
}

tasks.named("check") {
	dependsOn tasks.named("checkLint")
}

tasks.register("lint", JavaExec) {
	group = "formatting"
	description = "Fix Kotlin code style deviations."
	classpath = configurations.ktlint
	mainClass = "com.pinterest.ktlint.Main"
	jvmArgs "--add-opens=java.base/java.lang=ALL-UNNAMED"
	args "-F", "src/**/*.kt", "**.kts", "!**/build/**"
}

task installGitHooks(type: Copy) {
	from new File(rootProject.rootDir, 'hooks/pre-commit')
	into { new File(rootProject.rootDir, '.git/hooks') }
}

task setupNewrelic(type: Download) {
	mkdir 'newrelic'
	src 'https://download.newrelic.com/newrelic/java-agent/newrelic-agent/current/newrelic-java.zip'
	dest file('newrelic/newrelic-java.zip')
	doLast {
		copy {
			from zipTree(file('newrelic/newrelic-java.zip'))
			into rootDir
		}
	}
}

build.dependsOn installGitHooks
